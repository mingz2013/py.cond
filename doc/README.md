# py.cond


## 实现功能说明

本项目，主要目的，是实现一个内置脚本解析器，执行器。

输入的脚本为字符串类型，一般用于配置到json数据结构中的value中，所以整个脚本应当可以写到一行里面。
也就是需要一个语句的分隔符，可以放多条语句。


需要支持的数据类型：整数，小数，字符串，数组，时间



需要支持的运算操作：+, -, * , /, %, 

需要支持的条件操作：==, >=, <=, >, <, !=, 

需要支持的连接操作：&&, ||, 


需要的分隔符: (,),;,


可以定义变量，需要标识符。需要符号表管理。


脚本的最后一句的结果，直接作为整个程序的返回输出。一般应用也就是一条语句。


执行器部分，需要支持提前设置全局的变量生成方式。


## 设计概述
脚本可以分多行，或由";"分割写到一行。
整个脚本程序是一个utf8类型的字符串。

src->词法分析器，生成token流 -> 语法分析，生成AST -> AST执行 -> 结果返回。 



## EBNF

语言设计采用EBNF语法设计。

|EBNF元符号|含义                |
|:--------|:-------------------|
| ::=     | 定义为，推导为       |
| &#124;  | 或                 | 
| {}      | 含0次在内任意多次重复 |
| []      | 含0次和1次          |
| ()      | 括号内看作一项       |
| .       | 一条生成规则的结束   |
| <>      | 非终结符            |
| ""      | 终结符              |



## 词法定义
源码应当为utf8字符串。

源码中所有的空格符，制表符，换行等空白部分，都当做空格符处理，只用于源码中的单词的分割，和源码的可读性。


### 关键字

```bnf
<or关键字> ::= "or"
<and关键字> ::= "and"
<not关键字> ::= "not"
<in关键字> ::= "in"
<is关键字> ::= "is"
<False关键字> ::= "False"
<True关键字> ::= "True"
<None关键字> ::= "None"
```


### 标识符
```bnf
<标识符> ::= <非数字>{<数字>|<非数字>}
<数字> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<非数字> ::= <下划线>|<字母>
<下划线> ::= "_"
<字母> ::= <小写字母> | <大写字母>
<小写字母> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g"
        | "h" | "i" | "j" | "k" | "l" | "m" | "n"
        | "o" | "p" | "q" | "r" | "s" | "t"
        | "u" | "v" | "w"
        | "x" | "y" | "z"

<大写字母> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G"
        | "H" | "I" | "J" | "K" | "L" | "M" | "N"
        | "O" | "P" | "Q" | "R" | "S" | "T"
        | "U" | "V" | "W"
        | "X" | "Y" | "Z"
```


### 字面值

```bnf
<字面值> ::= <数字字面值> | <字符串字面值>
```

#### 数字字面值的定义

这里的数字，只支持基本的整数和小数，常见的场景。不支持一些非常见的场景。

```bnf
<数字字面值> ::= <整数字面值>|<小数字面值>
<整数字面值> ::= <数字>{<数字>}
<小数字面值> ::= <整数字面值><点号><整数字面值>
<数字> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<点号> ::= "."
```



#### 字符串字面值的定义

字符串的字面值，应当以双引号包括起来的字符串。


简单起见，只支持双引号的。

```bnf
<字符串字面值> ::= <双引号>{<字符串中的字符>}<双引号>
<双引号> ::= "\""
<字符串中的字符> ::= <转义字符>| "除双引号字符\",反斜线字符\\或换行符外的任何字符"
<转义字符> ::= "\'"|"\""|"\a"|"\b"|"\f"|"\n"|"\r"|"\t"|"\v"|"\0"|"\\"

```


### 运算符

```bnf
<加号> ::= "+"
<减号> ::= "-"
<星号> ::= "*"
<除号> ::= "/"
<取余号> ::= "%"

<等于号> ::= "=="
<不等于号> ::= "!="
<小于号> ::= "<"
<小于等于号> ::= "<="
<大于号> ::= ">"
<大于等于号> ::= ">="


```

### 分隔符
```bnf
<赋值等号> ::= "="
<点号> ::= "."
<左小括号> ::= "("
<右小括号> ::= ")"
<左中括号> ::= "["
<右中括号> ::= "]"
<分号> ::= ";"
<逗号> ::= ","
<单引号> ::= "'"
<双引号> ::= "\""
```



## 表达式


### 原子
```bnf
<原子> ::= <标识符> | <字面值> | <列表显示> 
            | <左小括号><布尔运算表达式><右小括号>
```

### 表达式
```bnf
<表达式> ::= <列表显示> | <布尔运算表达式>
```

### 表达式列表
```bnf

<表达式列表> ::= <表达式>{<逗号><表达式>}[<逗号>]

```

### 列表的显示

参考python的列表

```bnf

<列表显示> ::= <左中括号>[<表达式列表>]<右中括号>

```

## 原型
原型代表编程语言中最紧密绑定的操作。
```bnf
<原型> ::= <原子> | <属性调用>| <调用>| <抽取>
```

### 属性调用
```bnf
<属性调用> ::= <原型> <点号><标识符>
```

### 抽取
```bnf
<抽取> ::= <原型> <左中括号> <表达式列表><右中括号>
```

### 调用
```bnf
<调用> ::= <原型><左小括号>[<参数列表>]<右小括号>
<参数列表> ::= <参数>{<逗号><参数>}[<逗号>]
<参数> ::= <表达式>
```


### 一元运算符

负数

```bnf
<一元运算表达式> ::= <原型> | <减号><一元运算表达式>|<加号><一元运算表达式>
```

### 二元运算符

乘除类，加减类

```bnf
<乘除类运算表达式> ::= <一元运算表达式> | <乘除类运算表达式><乘号><一元运算表达式> | <乘除类运算表达式><除号><一元运算表达式> 
<加减类运算表达式> ::= <乘除类运算表达式> | <加减类运算表达式><加号><乘除类运算表达式>|<加减类运算表达式><减号><乘除类运算表达式>
<二元运算表达式> ::= <加减类运算表达式>
```

### 比较运算

这里参考python的比较运算


```bnf
<比较运算表达式> ::= <二元运算表达式> {<比较运算符><二元运算表达式>}
<比较运算符> ::= <等于号>|<不等于号>|<小于号>|<小于等于号>|<大于号>|<大于等于号>
                | <is关键字> [<not关键字>]| <not关键字> <in关键字>
<等于号> ::= "=="
<不等于号> ::= "!="
<小于号> ::= "<"
<小于等于号> ::= "<="
<大于号> ::= ">"
<大于等于号> ::= ">="

```

### 布尔运算
```bnf
<or运算表达式> ::= <and运算表达式>| <or运算表达式> <or关键字> <and运算表达式>
<and运算表达式> ::= <not运算表达式> | <and运算表达式> <and> <not运算表达式>
<not运算表达式> ::= <比较运算表达式> | <not关键字> <not运算表达式>
<布尔运算表达式> ::= <or运算表达式>
```






## 简单语句
简单语句由一个单独的逻辑构成，多条简单语句可以存在于同一行内并以分号分割。


```bnf
<简单语句> ::= <表达式语句>|<赋值语句>
```

### 表达式语句
```bnf
<表达式语句> ::= <表达式>
```

### 赋值语句

目前，赋值语句，只支持单个的赋值，不支持多个的同时赋值

```bnf
<赋值语句> ::= <标识符> <赋值等号> <表达式>
```



## 复合语句
一行里面可以有多条语句
最后的分号可有可无
```bnf
<复合语句> ::= <简单语句> {<分号><简单语句>}[<分号>]
```


## 最高层级组件

```bnf
<文件> ::= {<复合语句>}
```


# 参考
## sites
- https://docs.python.org/zh-cn/3/reference/index.html
- https://docs.python.org/zh-cn/3/reference/lexical_analysis.html
- https://docs.python.org/zh-cn/3/reference/expressions.html
- https://docs.python.org/zh-cn/3/reference/simple_stmts.html
- https://docs.python.org/zh-cn/3/reference/compound_stmts.html
- https://docs.python.org/zh-cn/3/reference/toplevel_components.html

## books
- 《自己动手写编译器、链接器》
